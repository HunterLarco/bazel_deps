# -*- python -*-

# Copyright 2018 Josh Pieper, jjp@pobox.com.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@com_github_mjbots_bazel_deps//tools/workspace:template_file.bzl",
     "template_file")
load("@com_github_mjbots_bazel_deps//tools/workspace:generate_file.bzl",
     "generate_file")
load("@com_github_mjbots_bazel_deps//tools/workspace/ffmpeg:config.bzl",
     "flatten_config")
load("@com_github_mjbots_bazel_deps//tools/workspace/nasm:nasm.bzl",
     "nasm_objects")

# TODO(jpieper): This could be refactored even more for reduced
# redundancy.  Particularly, we could have internal cc_library rules
# for the compilation of each module that exported every header, then
# the external ones which reference the .so and only the public
# headers.

package(default_visibility = ["//visibility:public"])

cc_library(
    name = "ffmpeg",
    deps = [
        ":swscale",
        ":swresample",
        ":avformat",
        ":avfilter",
        ":avdevice",
        ":avcodec",
    ],
)

CONFIG_HEADERS = [
    "private/avversion.h",
    "private/config.h",
]

COMMON_COPTS = [
    "-I$(GENDIR)/external/ffmpeg/private",
    "-Iexternal/ffmpeg/private",
    "-DHAVE_AV_CONFIG_H",
    "-DPIC",

    "-Wno-deprecated-declarations",
    "-Wno-unused-function",
] + select({
    "@com_github_mjbots_bazel_deps//conditions:gcc" : [
        "-Wno-discarded-qualifiers",
    ],
    "@com_github_mjbots_bazel_deps//conditions:clang" : [
        "-Wno-shift-negative-value",
        "-Wno-incompatible-pointer-types-discards-qualifiers",
        "-Wno-constant-conversion",
        "-Wno-unused-const-variable",
        "-Wno-#warnings",
    ],
    "//conditions:default" : [],
})

cc_library(
    name = "avutil",
    hdrs = @AVUTIL_HEADERS@,
    srcs = [":libavutil.so"],
    includes = ["."],
)

cc_library(
    name = "avutil_textual_headers",
    textual_hdrs = ["libavutil/" + x for x in [
        "log2_tab.c",
        "reverse.c",

        "arm/asm.S",
    ]],
)

cc_library(
    name = "compat",
    hdrs = [
        "compat/va_copy.h",
        "compat/nvenc/nvEncodeAPI.h",
        "compat/cuda/dynlink_loader.h",
        "compat/cuda/dynlink_cuda.h",
        "compat/cuda/dynlink_nvcuvid.h",
        "compat/cuda/dynlink_cuviddec.h",
        "compat/w32dlfcn.h",
    ],
)

cc_binary(
    name = "libavutil.so",
    linkshared = True,
    srcs = ["libavutil/" + x for x in [
        "avconfig.h",
    ]] + [
        # Yay, fix up some circular header dependencies.
        "libavfilter/bufferqueue.h",
        "libavfilter/avfilter.h",
        "libavfilter/version.h",
        "libavfilter/window_func.h",
        "libavcodec/mathops.h",
        "libavcodec/x86/mathops.h",
        "libavcodec/arm/mathops.h",
    ] + @AVUTIL_SOURCES@ + glob(["libavutil/**/*.h"]) + CONFIG_HEADERS +select({
        "@com_github_mjbots_bazel_deps//conditions:arm" : @AVUTIL_ARM_SOURCES@,
        "@com_github_mjbots_bazel_deps//conditions:x86_64" : (@AVUTIL_X86_SOURCES@ + [":avutil_x86asm"]),
    }),
    copts = COMMON_COPTS + [
        "-Wno-pointer-sign",
        "-Wno-incompatible-pointer-types",
        "-Wno-parentheses",
        "-Wno-switch",
    ],
    includes = ["."],
    linkopts = [
        "-Wl,-Bsymbolic",
        "-Wl,--version-script,$(location libavutil/libavutil.lds)",
    ],
    deps = [
        ":compat",
        ":libavutil/libavutil.lds",
        ":avutil_textual_headers",
    ],
)

generate_file(
    name = "libavutil/avconfig.h",
    content = """
/* Generated by ffconf */
#ifndef AVUTIL_AVCONFIG_H
#define AVUTIL_AVCONFIG_H
#define AV_HAVE_BIGENDIAN 0
#define AV_HAVE_FAST_UNALIGNED 0
#endif /* AVUTIL_AVCONFIG_H */
    """,
)

cc_library(
    name = "swscale",
    hdrs = @SWSCALE_HEADERS@,
    srcs = [":libswscale.so"],
    includes = ["."],
    deps = [":avutil"],
)

cc_library(
    name = "swscale_textual_headers",
    textual_hdrs = ["libswscale/" + x for x in [
        "rgb2rgb_template.c",
        "bayer_template.c",
        "x86/rgb2rgb_template.c",
        "arm/rgb2yuv_neon_common.S",
    ]] + [
        "libavutil/log2_tab.c",
    ],
)

cc_binary(
    name = "libswscale.so",
    linkshared = True,
    srcs = @SWSCALE_SOURCES@ + glob(["libswscale/**/*.h", "libavutil/**/*.h"]) + CONFIG_HEADERS + select({
        "@com_github_mjbots_bazel_deps//conditions:arm" : @SWSCALE_ARM_SOURCES@,
        "@com_github_mjbots_bazel_deps//conditions:x86_64" : (@SWSCALE_X86_SOURCES@ + [":swscale_x86asm"]),
    }),
    copts = COMMON_COPTS + [
        "-Wno-switch",
        "-Wno-unused-function",
        "-Wno-incompatible-pointer-types",
        "-Wno-parentheses",
    ],
    deps = [
        ":avutil",
        ":libswscale/libswscale.lds",
        ":swscale_textual_headers",
    ],
    linkopts = [
        "-Wl,-Bsymbolic",
        "-Wl,--version-script,$(location libswscale/libswscale.lds)",
    ],
)

cc_library(
    name = "swresample",
    hdrs = @SWRESAMPLE_HEADERS@,
    srcs = [":libswresample.so"],
    includes = ["."],
    deps = [":avutil"],
)

cc_library(
    name = "swresample_textual_headers",
    textual_hdrs = ["libswresample/" + x for x in [
        "noise_shaping_data.c",
        "resample_template.c",
        "dither_template.c",
        "rematrix_template.c",
        "log2_tab.c",
    ]] + [
        "libavutil/log2_tab.c",
    ],
)

cc_binary(
    name = "libswresample.so",
    linkshared = True,
    srcs = @SWRESAMPLE_SOURCES@ + glob(["libswresample/**/*.h"]) + CONFIG_HEADERS + select({
        "@com_github_mjbots_bazel_deps//conditions:arm" : @SWRESAMPLE_ARM_SOURCES@,
        "@com_github_mjbots_bazel_deps//conditions:x86_64" : (@SWRESAMPLE_X86_SOURCES@ + [":swresample_x86asm"]),
    }) + glob(["libavutil/**/*.h"]),
    copts = COMMON_COPTS + [
        "-Wno-pointer-sign",
        "-Wno-switch",
    ],
    deps = [
        ":avutil",
        ":libswresample/libswresample.lds",
        ":swresample_textual_headers",
    ],
    linkopts = [
        "-Wl,-Bsymbolic",
        "-Wl,--version-script,$(location libswresample/libswresample.lds)",
    ],
)

cc_library(
    name = "avcodec",
    hdrs = @AVCODEC_HEADERS@,
    srcs = [":libavcodec.so"],
    includes = ["."],
    deps = [":avutil", ":swresample"],
)

cc_library(
    name = "avcodec_textual_headers",
    textual_hdrs = ["libavcodec/" + x for x in [
        "aacps.c",
        "ac3dec.c",
        "aacpsdata.c",
        "eac3dec.c",
        "codec_list.c",
        "parser_list.c",
        "x86/h264_cabac.c",
        "x86/rnd_template.c",
        "x86/vp9dsp_init_16bpp_template.c",
        "x86/hpeldsp_rnd_template.c",
        "x86/mpegvideoenc_qns_template.c",
        "arm/vp9dsp_init_16bpp_arm_template.c",
        "arm/neon.S",
    ]] + [
        ":libavcodec/bsf_list.c",
        "libavutil/log2_tab.c",
        "libavutil/reverse.c",
    ] + glob(["libavcodec/*_template.c"]),
)

cc_binary(
    name = "libavcodec.so",
    linkshared = True,
    srcs = @AVCODEC_SOURCES@ + CONFIG_HEADERS + ["libavcodec/" + x for x in [
        # HAVE_THREADS
        "pthread.c",
        "pthread_slice.c",
        "pthread_frame.c",
    ]] + glob(["libavcodec/**/*.h", "libavformat/**/*.h", "libavfilter/**/*.h", "libavutil/**/*.h"]) + select({
        "@com_github_mjbots_bazel_deps//conditions:arm" : @AVCODEC_ARM_SOURCES@,
        "@com_github_mjbots_bazel_deps//conditions:x86_64" : (@AVCODEC_X86_SOURCES@ + [":avcodec_x86asm"]),
    }),
    copts = COMMON_COPTS + [
        "-Wno-pointer-sign",
        "-Wno-parentheses",
        "-Wno-switch",
        "-Wno-incompatible-pointer-types",
        "-Iexternal/ffmpeg/libavcodec/",
    ],
    deps = [
        ":avutil",
        ":compat",
        ":swresample",
        ":libavcodec/libavcodec.lds",
        ":avcodec_textual_headers",
    ],
    linkopts = [
        "-Wl,-Bsymbolic",
        "-Wl,--version-script,$(location libavcodec/libavcodec.lds)",
    ],
)

cc_library(
    name = "avformat",
    hdrs = @AVFORMAT_HEADERS@,
    srcs = [":libavformat.so"],
    includes = ["."],
    deps = [":avutil", ":avcodec"],
)

cc_library(
    name = "avformat_textual_headers",
    textual_hdrs = [
        "libavformat/protocol_list.c",
        "libavformat/muxer_list.c",
        "libavformat/demuxer_list.c",
        "libavformat/log2_tab.c",
        "libavcodec/golomb.c",
        "libavutil/log2_tab.c",
    ],
)

cc_binary(
    name = "libavformat.so",
    linkshared = True,
    srcs = @AVFORMAT_SOURCES@ + CONFIG_HEADERS + glob(["libavformat/**/*.h", "libavutil/**/*.h", "libavcodec/**/*.h"]) + select({
        "@com_github_mjbots_bazel_deps//conditions:arm" : @AVFORMAT_ARM_SOURCES@,
        "@com_github_mjbots_bazel_deps//conditions:x86_64" : @AVFORMAT_X86_SOURCES@,
    }),
    copts = COMMON_COPTS + [
        "-Wno-parentheses",
        "-Wno-pointer-sign",
        "-Wno-switch",
        "-Iexternal/ffmpeg/libavcodec",
    ],
    deps = [
        "@bzip2",
        ":avutil",
        ":avcodec",
        ":libavformat/libavformat.lds",
        ":avformat_textual_headers",
    ],
    linkopts = [
        "-Wl,-Bsymbolic",
        "-Wl,--version-script,$(location libavformat/libavformat.lds)",
    ],
)

cc_library(
    name = "avfilter",
    hdrs = @AVFILTER_HEADERS@,
    srcs = [":libavfilter.so"],
    includes = ["."],
    deps = [":avutil", ":avcodec", ":avformat", ":swscale"],
)

cc_library(
    name = "avfilter_textual_headers",
    textual_hdrs = ["libavfilter/" + x for x in [
        "colorspacedsp_template.c",
        "colorspacedsp_yuv2yuv_template.c",
        "filter_list.c",
        "all_channel_layouts.inc",
    ]] + [
        "libavutil/log2_tab.c",
    ],
)

cc_binary(
    name = "libavfilter.so",
    linkshared = True,
    srcs = @AVFILTER_SOURCES@ + CONFIG_HEADERS + ["libavfilter/" + x for x in [
        "pthread.c",
    ]] + glob(["libavfilter/**/*.h", "libavcodec/**/*.h", "libavutil/**/*.h"]) + select({
        "@com_github_mjbots_bazel_deps//conditions:arm" : @AVFILTER_ARM_SOURCES@,
        "@com_github_mjbots_bazel_deps//conditions:x86_64" : (@AVFILTER_X86_SOURCES@ + [":avfilter_x86asm"]),
    }),
    copts = COMMON_COPTS + [
        "-Wno-switch",
        "-Wno-deprecated-declarations",
        "-Wno-incompatible-pointer-types",
        "-Wno-parentheses",
        "-Wno-pointer-sign",
        "-Iexternal/ffmpeg/libavfilter",
    ],
    deps = [
        ":avutil",
        ":avcodec",
        ":avformat",
        ":swscale",
        ":libavfilter/libavfilter.lds",
        ":avfilter_textual_headers",
    ],
    linkopts = [
        "-Wl,-Bsymbolic",
        "-Wl,--version-script,$(location libavfilter/libavfilter.lds)",
    ],
)

cc_library(
    name = "avdevice",
    hdrs = @AVDEVICE_HEADERS@,
    srcs = [":libavdevice.so"],
    includes = ["."],
    deps = [":avutil", ":avcodec", ":avformat", ":avfilter"],
)

cc_library(
    name = "avdevice_textual_headers",
    textual_hdrs = ["libavdevice/" + x for x in [
        "outdev_list.c",
        "indev_list.c",
    ]] + [
        "libavutil/reverse.c",
    ],
)

cc_binary(
    name = "libavdevice.so",
    linkshared = True,
    srcs = @AVDEVICE_SOURCES@ + CONFIG_HEADERS + glob(["libavdevice/**/*.h", "libavutil/**/*.h", "libavformat/**/*.h"]),
    copts = COMMON_COPTS + [
        "-Wno-pointer-sign",
    ],
    deps = [
        ":avutil",
        ":avcodec",
        ":avformat",
        ":avfilter",
        ":libavdevice/libavdevice.lds",
        ":avdevice_textual_headers",
    ],
    linkopts = [
        "-Wl,-Bsymbolic",
        "-Wl,--version-script,$(location libavdevice/libavdevice.lds)",
    ],
)

[nasm_objects(
    name = module + "_x86asm",
    srcs = select({
        "@com_github_mjbots_bazel_deps//conditions:x86_64" : asm_files,
        "//conditions:default" : [],
    }),
    textual_hdrs = [
        "libavutil/x86/x86util.asm",
        "libavutil/x86/x86inc.asm",
        "libavcodec/x86/vp9itxfm_template.asm",
        "libavcodec/x86/simple_idct10_template.asm",
        "libavcodec/x86/vp9itxfm_16bpp.asm",
    ],
    force_includes = [
        "private/config.asm",
    ],
    extra_args = [
        "-f", "elf64",
        "-g",
        "-DPIC",
        "-F", "dwarf",
        "-Iexternal/ffmpeg/",
        "-Iexternal/ffmpeg/lib{}/x86/".format(module),
    ],
) for module, asm_files in [
    ("swresample", @SWRESAMPLE_X86_X86ASM@),
    ("swscale", @SWSCALE_X86_X86ASM@),
    ("avutil", @AVUTIL_X86_X86ASM@),
    ("avcodec", @AVCODEC_X86_X86ASM@),
    ("avformat", @AVFORMAT_X86_X86ASM@),
    ("avfilter", @AVFILTER_X86_X86ASM@),
    ("avdevice", @AVDEVICE_X86_X86ASM@),
]]

generate_file(
    name = "private/avversion.h",
    content = """
#define LIBAV_VERSION "12.3"
    """,
)

[genrule(
    name = "{x}_linkerscript".format(x=module),
    outs = ["lib{x}/lib{x}.lds".format(x=module)],
    srcs = ["lib{x}/lib{x}.v".format(x=module)],
    cmd = "cp $< $@",
) for module in @MODULES@]

template_file(
    name = "private/config.asm",
    src = "private/config.h",
    substitutions = {
        "#define" : "%define",
        "#endif" : "",
        "#ifndef FFMPEG_CONFIG_H" : "",
        "#define FFMPEG_CONFIG_H" : "",
    },
)

X86_OPTS = [
    "MMX",
    "SSE",
    "SSE2",
    "SSE3",
    "SSE4",
    "SSE42",
    "AVX",
    "AVX2",
]

ARM_OPTS = [
    "NEON",
    "ARMV6",
    "ARMV6T2",
]

template_file(
    name = "private/config.h",
    src = "private/config.h.in",
    substitution_list = select({
        "@com_github_mjbots_bazel_deps//conditions:arm" : ([
            "#define ARCH_ARM 0=#define ARCH_ARM 1",
        ] + [
            "#define HAVE_{x} 0=#define HAVE_{x} 1".format(x=x)
            for x in ARM_OPTS
        ] + [
            "#define HAVE_{x}_EXTERNAL 0=#define HAVE_{x}_EXTERNAL 1".format(x=x)
            for x in ARM_OPTS
        ]),
        "@com_github_mjbots_bazel_deps//conditions:x86_64" : ([
            "#define ARCH_X86 0=#define ARCH_X86 1",
            "#define ARCH_X86_64 0=#define ARCH_X86_64 1",
            "#define HAVE_X86ASM 0=#define HAVE_X86ASM 1",
        ] + [
            "#define HAVE_{x} 0=#define HAVE_{x} 1".format(x=x)
            for x in X86_OPTS
        ] + [
            "#define HAVE_{x}_EXTERNAL 0=#define HAVE_{x}_EXTERNAL 1".format(x=x)
            for x in X86_OPTS
        ]),
    }),
)


generate_file(
    name = "libavutil/ffversion.h",
    content = """
/* Automatically generated by version.sh, do not manually edit! */
#ifndef AVUTIL_FFVERSION_H
#define AVUTIL_FFVERSION_H
#define FFMPEG_VERSION "3.4.2"
#endif /* AVUTIL_FFVERSION_H */
""")
